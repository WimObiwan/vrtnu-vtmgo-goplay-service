buildscript {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath Plugins.kotlinGradle
        classpath Plugins.versions
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://kotlin.bintray.com/kotlinx/" }
        maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
        maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local/" } // for SNAPSHOT builds
    }

    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: "com.github.ben-manes.versions"
    apply from: "${rootDir}/credentials.gradle"

    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            def version = it.candidate.version
            def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
            def regex = /^[0-9,.v-]+(-r)?$/
            return !stableKeyword && !(version ==~ regex)
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    test {
        addAuthenticationTokensToSystemEnv(it)
        useJUnitPlatform()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.5"
        }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    "-Xinline-classes",
                    "-Xexplicit-api=strict",
                    "-Xopt-in=kotlin.RequiresOptIn",
                    "-Xopt-in=be.tapped.common.internal.InterModuleUseOnly",
                    //"-Xuse-ir"
            ]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    "-Xopt-in=kotlin.RequiresOptIn",
                    "-Xopt-in=be.tapped.common.internal.InterModuleUseOnly",
                    //"-Xuse-ir"
            ]
        }
    }
}
